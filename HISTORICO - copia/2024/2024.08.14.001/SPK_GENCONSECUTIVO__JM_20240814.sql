IF EXISTS (SELECT NAME FROM SYSOBJECTS WHERE NAME = 'SPK_GENCONSECUTIVO' AND TYPE = 'P')
   DROP PROCEDURE SPK_GENCONSECUTIVO

GO
CREATE PROCEDURE DBO.SPK_GENCONSECUTIVO  
	@COMPANIA  VARCHAR(2),  
	@SEDE      VARCHAR(5),  
	@PREFIJO   VARCHAR(20),   
	@NVOCONSEC VARCHAR(20) OUTPUT
WITH ENCRYPTION
AS  
	DECLARE @EXISTE INT
	DECLARE @RETRY_COUNT_CURRENT INT
	DECLARE @RETRY_COUNT_MAXIMUN INT
	DECLARE @ERROR_NUM INT
	DECLARE @ERROR_MSG NVARCHAR(MAX)
BEGIN
	
	IF @PREFIJO IS NULL
	BEGIN
		PRINT 'ES NULL EL PREFIJO'
	END
   IF COALESCE(@SEDE,'')='' OR @SEDE IS NULL
   BEGIN
      SELECT @SEDE=LTRIM(RTRIM(DBO.FNK_VALORVARIABLE('IDSEDEPRINCIPAL')))
   END
   IF @PREFIJO NOT LIKE '@FTR%'
	BEGIN
		EXEC SPQ_GENSEQUENCE @SEDE = @SEDE, @PREFIJO = @PREFIJO, @LLAMADO = 'ANT', @NVOCONSEC = @NVOCONSEC OUTPUT
	END
	ELSE
	BEGIN


		SET  @ERROR_NUM = 0
		SET  @RETRY_COUNT_CURRENT = 0
		SET  @RETRY_COUNT_MAXIMUN = 3

		WHILE @RETRY_COUNT_CURRENT < @RETRY_COUNT_MAXIMUN
		BEGIN
			DECLARE @trancount int = @@trancount
			IF @trancount = 0
				BEGIN TRANSACTION
			ELSE
				SAVE TRANSACTION USP_SPK_GENCONSECUTIVO

			BEGIN TRY
				SELECT @EXISTE = COUNT(*) FROM USCXS
				WHERE COMPANIA = @COMPANIA  
				AND   IDSEDE   = @SEDE  
				AND   PREFIJO  = @PREFIJO  

				IF @EXISTE  > 0  
				BEGIN
					UPDATE USCXS SET CONSECUTIVO = CONSECUTIVO + 1  
					WHERE COMPANIA = @COMPANIA  
					AND   IDSEDE   = @SEDE  
					AND   PREFIJO  = @PREFIJO  
				END
				ELSE
				BEGIN   
					INSERT INTO USCXS (COMPANIA,IDSEDE,BODEGA,IDADICIONAL,PREFIJO,CONSECUTIVO,SERIE,LIMITE)  
					VALUES(@COMPANIA,@SEDE,'','',@PREFIJO,1,0,0)  
				END 
			
				SELECT @NVOCONSEC = CONSECUTIVO FROM USCXS  
				WHERE  COMPANIA   = @COMPANIA  
				AND    IDSEDE     = @SEDE  
				AND    PREFIJO    = @PREFIJO

				IF @PREFIJO = '@MCP'
				BEGIN
					-- AGREGADO POR R.JAY 2009.04.18 PARA MEJORAR LA CREACION DE LOS CONSECUTIVOS CONTABLE {NROCOMPROBANTE}
					-- ESTO AYUDARA EN LOS PROCESOS DE ENVIO MASIVO A CONTABILIDAD
					-- CORRECCION REALIZADA POR R.JAY 2009.06.26 PARA SEDE DE MAS DE 2 DIGITOS
					-- SELECT @NVOCONSEC = CONVERT(VARCHAR,@@SPID) + REPLACE(SPACE(17-LEN(@@SPID)-LEN(@NVOCONSEC))+LTRIM(RTRIM(@NVOCONSEC)),SPACE(1),0)
					SELECT @NVOCONSEC = CONVERT(VARCHAR,@@SPID) + REPLACE(SPACE(14-LEN(@@SPID)-LEN(@NVOCONSEC))+LTRIM(RTRIM(@NVOCONSEC)),SPACE(1),0)
				END
				IF @trancount = 0 COMMIT TRANSACTION
				BREAK
			END TRY
			BEGIN CATCH
				IF ERROR_NUMBER() = 1205
				BEGIN
					--ROLLBACK
					IF @trancount = 0 ROLLBACK TRANSACTION
					ELSE
						IF XACT_STATE() <> -1
							ROLLBACK TRANSACTION USP_SPK_GENCONSECUTIVO

					SET @RETRY_COUNT_CURRENT += 1
					WAITFOR DELAY '00:00:02'
					CONTINUE
				END
				ELSE
				BEGIN 
					--ROLLBACK
					IF @trancount = 0 ROLLBACK TRANSACTION
					ELSE
						IF XACT_STATE() <> -1
							ROLLBACK TRANSACTION USP_SPK_GENCONSECUTIVO
					BREAK
				END
			END CATCH
		END
	END
END





