CREATE OR ALTER PROCEDURE DBO.SPK_GENERA_TOKEN_WEB @TRANSIENT VARCHAR(20),@OK VARCHAR(4) OUTPUT,  @JWT VARCHAR(MAX) OUTPUT
	WITH ENCRYPTION
AS
SET DATEFORMAT dmy
DECLARE @JWT_QRYSTALOS VARCHAR(MAX)
DECLARE @TABLA_TMP AS TABLE (ITEM INT IDENTITY(1,1), RESPONSE VARCHAR(MAX))
DECLARE @sUrl VARCHAR(3096) 
DECLARE @CREDENCIAL VARCHAR(100)
DECLARE @CIA VARCHAR(2) 
DECLARE @USER VARCHAR(20) 
DECLARE @CLAVE VARCHAR(20) 
DECLARE @obj INT
DECLARE @response VARCHAR(max)
DECLARE @Body VARCHAR(max) 
BEGIN
   PRINT 'EXEC SPK_GENERA_TOKEN_WEB '+@TRANSIENT
   --https://api-test.qrystalos.com/api/ususu/ingresar
   IF DB_NAME()=DBO.FNK_VALORVARIABLE('BDATA_PRODUCCION')
   BEGIN
      SELECT @sUrl=LTRIM(RTRIM(DBO.FNK_VALORVARIABLE('URL_API_QRYSTAL_PROD')))+'/ususu/ingresar'
      SELECT @CREDENCIAL=LTRIM(RTRIM(TRIM(DBO.FNK_VALORVARIABLE('USER_API_PROD'))))
   END
   ELSE
   BEGIN
      SELECT @sUrl=LTRIM(RTRIM(DBO.FNK_VALORVARIABLE('URL_API_QRYSTAL_TEST')))+'/ususu/ingresar'
      SELECT @CREDENCIAL=LTRIM(RTRIM(TRIM(DBO.FNK_VALORVARIABLE('USER_API_TEST'))))
   END
   SELECT @CIA=WORD FROM DBO.FNK_EXPLODE(@CREDENCIAL,',') WHERE WORDID=1
   SELECT @USER=WORD FROM DBO.FNK_EXPLODE(@CREDENCIAL,',') WHERE WORDID=2
   SELECT @CLAVE=WORD FROM DBO.FNK_EXPLODE(@CREDENCIAL,',') WHERE WORDID=3

   IF COALESCE(@sUrl,'')='' OR LEN(@sUrl)<15
   BEGIN
      SELECT  @OK='KO', @JWT='ERROR Url No Valida'
      RETURN
   END
   IF COALESCE(@CIA,'')='' OR LEN(@CIA)<>2 OR COALESCE(@USER,'')='' OR COALESCE(@CLAVE,'')='' OR LEN(@CLAVE)<6
   BEGIN
      PRINT @CREDENCIAL
      PRINT @CIA
      PRINT @USER
      PRINT @CLAVE
      SELECT  @OK='KO', @JWT='Credenciales de Acceso Imcompletas'
      RETURN
   END
   SELECT @Body='{"COMPANIA": "'+@CIA+'", "USUARIO": "'+@USER+'", "CLAVE": "'+@CLAVE+'"}'

   EXEC  dbo.SPK_GET_TRANSIENT 	@TRANSIENT =  @TRANSIENT ,	@DATO = @JWT_QRYSTALOS OUT
   IF COALESCE(@JWT_QRYSTALOS,'')=''
   BEGIN
	
      EXEC sys.sp_OACreate 'MSXML2.ServerXMLHttp', @obj OUT

	   EXEC sys.sp_OAMethod @obj, 'Open', NULL, 'POST', @sUrl, false
	   EXEC sys.sp_OAMethod @Obj, 'setRequestHeader', null, 'Content-Type', 'application/json'
	   EXEC SYS.sp_OAMethod @obj, 'send', null, @Body
	   EXEC sys.sp_OAMethod @obj, 'responseText', @response OUTPUT

	   IF @response IS NULL
	   BEGIN
		   INSERT INTO @TABLA_TMP(RESPONSE)
		   EXEC sys.sp_OAGetProperty @obj, 'responseText'
		   SELECT @response=RESPONSE FROM @TABLA_TMP
	   END

	   EXEC sys.sp_OADestroy @obj
   
      IF ISJSON(@RESPONSE)=1
      BEGIN
         SELECT @JWT_QRYSTALOS = JSON_VALUE(@response,'$.jwt')
      END
   END
   IF COALESCE(@JWT_QRYSTALOS,'') <> ''
   BEGIN
      EXEC dbo.SPK_SET_TRANSIENT @TRANSIENT = @TRANSIENT, @VALUE = @JWT_QRYSTALOS, @EXPIRATION = 360 -- 6 HORAS

      SELECT @OK='OK', @JWT = 'Bearer ' + @JWT_QRYSTALOS
      RETURN
   END
   ELSE
   BEGIN
      SELECT  @OK='KO', @JWT='JWT no fue Generado Correctamente'
      RETURN
   END
END